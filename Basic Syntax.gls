comment block : (Basic GLS Syntax) (Version 0.0.1) (Josh Goldberg)
file start : SampleFile
    comment line : (Function Definitions)
    function start : sayHello void
        print line : ("Hello world!")
    function end
    function start : combineStrings string a string b string
        function return : { operation : a plus b }
    function end

    comment line : (Class Declarations)
    class start : Point
        class member variable declare : x public int
        class member variable declare : y public int

        class constructor start : Point x int y int
            class member variable set : x x
            class member variable set : y y
        class constructor end 
    
        class member function start : Point public setX void x int
            class member variable set : x x
        class member function end
    
        class member function start : Point public setY void y int
            class member variable set : y y
        class member function end
    
        class member function start : Point public getX int
            function return : { class member variable get : x}
        class member function end
    
        class member function start : Point public getY int
            function return : { class member variable get : y}
        class member function end
    
        class member function start : Point public getManhattanTotal int
            function return : { operation : { class member variable get : x } plus { class member variable get : y } }
        class member function end
    class end

    comment line : Main
    main start
        comment line : (Basic Usage)
        print line : ("Hello world!")
        comment inline : (Basic printing here...)

        comment line : Variables
        variable declare : a string ("Hello world!")
        variable declare : b int 7
        variable declare : c double 11.7
        variable declare : d boolean true

        comment line : Operations
        variable declare : e int { operation : 1 plus 2 } 
        variable declare : f boolean { comparison : b lessthan c }

        comment line : (If Statements)
        if variable start : d
            print line : ("d is true!")
        if end
        if condition start : c lessthan 14
            print line : ("c is less than 14!")
        if end

        comment line : (While Loops)
        while variable start : d
            print line : { concatenate : ("d is ") d }
            operation : d equals false
        while end
        while condition start : c greaterthan 3
            print line : { concatenate : ("c is ") c }
            operation : c decreaseby 1
        while end

        comment line : (For Loops)
        for numbers start : i int 0 lessthan 7 increaseby 1
            print line : { concatenate ("i plus one is ") { operation : i plus 1 } }
        for end

        comment line : (Calling Functions)
        function call : sayHello
        function call : combineStrings "hello" "world"
        function call : combineStrings { operation : "hello" plus (" ") } "world"
        function call : combineStrings { function call : combineStrings "hello" "world" } "world"

        comment line : (Class Usage)
        variable declare : g Point { class new : Point 3 7 }
        class member function call : g setX 4
        print line : { class member function call : g getManhattanTotal }
    main end
file end

comment line : (fin)